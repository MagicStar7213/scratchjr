apply plugin: 'com.android.application'

android {
    compileSdk 33
    buildToolsVersion '33.0.1'

    defaultConfig {
        applicationId "org.scratchjr.android"
        manifestPlaceholders = [disableAnalytics: "true"]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'scratchjrversion'
    productFlavors {
        free {
            dimension = 'scratchjrversion'
            applicationId "org.scratchjr.androidfree"
            minSdkVersion 21
            targetSdkVersion 29
            versionCode 22
            versionName "1.2.11"
            manifestPlaceholders = [disableAnalytics: "true"]
        }
    }

    // The camera-view dependency contains Java 8 bytecode,
    // we need to support java 8 to dex.
    // See https://developer.android.com/studio/write/java8-support.html for details.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.google.firebase:firebase-core:21.1.1'
    implementation 'com.google.firebase:firebase-analytics:21.2.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'

    implementation 'androidx.appcompat:appcompat:1.5.1'
    def camerax_version = "1.0.0-rc05"
    // CameraX core library using camera2 implementation
    implementation 'androidx.camera:camera-camera2:1.2.0'
    // CameraX Lifecycle Library
    implementation 'androidx.camera:camera-lifecycle:1.2.0'
    // CameraX View class
    implementation 'androidx.camera:camera-view:1.2.0'
}

def appModuleRootFolder = '.'
def srcDir = 'src'
def googleServicesJson = 'google-services.json'
def commonHome = '../../..'
def html5Assets = 'src/main/assets/HTML5'

task switchToFree(type: Copy) {
    def buildType = 'free'
    from "${commonHome}/editions/${buildType}/src"
    into html5Assets
}

task switchToFreeGA(type: Copy) {
    def buildType = 'free'
    from "${commonHome}/editions/${buildType}/android-resources"
    include "$googleServicesJson"
    into "$appModuleRootFolder"
}

task generateWebpackBundle(type: Exec) {
    workingDir '../../../bin'
    commandLine './bundle-compile.sh'
}

class GenerateScratchJrPNGsTask extends DefaultTask {
    @InputDirectory
    File commonHome = project.file("../../..")
    @InputDirectory
    File binDir = project.file("${commonHome}/bin")

    @OutputDirectory
    File generatedAssets = project.file("src/main/assets")
    @OutputDirectory
    File generatedHtml5Assets = project.file("${generatedAssets}/HTML5")
    @OutputDirectory
    File pngLibrary = project.file("${generatedHtml5Assets}/pnglibrary")
    @OutputDirectory
    File svgLibrary = project.file("${generatedHtml5Assets}/svglibrary")

    @TaskAction
    def go() {
        pngLibrary.mkdirs()
        ant.exec(executable: "${binDir}/convert-svg-to-png.py",
            failOnError: true)
        {
            arg(value: "-i ${svgLibrary}/")
            arg(value: "-o ${pngLibrary}/")
        }
    }
}
class CleanScratchJrResourcesTask extends DefaultTask {
    File generatedAssets = project.file('src/main/assets')

    @TaskAction
    def go() {
        generatedAssets.deleteDir()
    }
}

task generateScratchJrPNGs(type: GenerateScratchJrPNGsTask)
task cleanScratchJrResources(type: CleanScratchJrResourcesTask)

afterEvaluate {
    preBuild.dependsOn generateWebpackBundle

    generateFreeReleaseResources.dependsOn switchToFree
    generateFreeDebugResources.dependsOn switchToFree
    generateFreeReleaseResources.dependsOn generateScratchJrPNGs
    generateFreeDebugResources.dependsOn generateScratchJrPNGs
    generateScratchJrPNGs.mustRunAfter switchToFree
}

clean.dependsOn('cleanScratchJrResources')
